# a1a8143ac967d7c716cfc0998123f9fd334c4dafebbebbd9443d96f3cc2707a6 Elan Beta 1 valid

main
  # Minimal example - 1 rule, writes * onto empty tape and halts
  var tm set to new TuringMachine("init", "halt")
  call tm.add(new Rule("init", " ", "halt", "*", Dir.right))
  call tm.runToEnd()
  print tm.tape
end main

class TuringMachine
  constructor(initialState as String, haltState as String)
    set property.initialState to initialState
    set property.haltState to haltState
    set rules to empty {Rule}
    set currentState to initialState
    set headPosition to 0
    set tape to " "
  end constructor

  property initialState as String

  property currentState as String

  property headPosition as Int

  property haltState as String

  property rules as {Rule}

  property tape as String

  procedure setTape(tape as String)
    set property.tape to tape
  end procedure

  procedure setRules(rules as {Rule})
    set property.rules to rules
  end procedure

  procedure add(rule as Rule)
    set rules to rules + rule
  end procedure

  procedure reset()
    set currentState to initialState
    set headPosition to 0
  end procedure

  procedure singleStep()
    var rule set to findMatchingRule()
    call execute(rule)
  end procedure

  procedure runToEnd()
    while not isHalted()
      call singleStep()
    end while
  end procedure

  function isHalted() return Boolean
    return currentState is haltState
  end function

  function getTrimmedTape() return String
    return tape.trim()
  end function

  function findMatchingRule() return Rule
    let matches be rules.filter(lambda r as Rule => (r.currentState is currentState) and (r.currentSymbol is tape[headPosition]))
    if matches.length() is 0
      then
        throw "No rule matching state {currentState} and symbol {tape[headPosition]}"
    end if
    return matches.head()
  end function

  procedure write(newSymbol as String)
    var hp set to headPosition
    set tape to tape[..hp] + newSymbol + tape[hp + 1..]
  end procedure

  procedure execute(rule as Rule)
    set currentState to rule.nextState
    call write(rule.writeSymbol)
    if rule.move is Dir.right
      then
        set headPosition to headPosition + 1
        if headPosition >= tape.length()
          then
            set tape to tape + " "
        end if
      else
        set headPosition to headPosition - 1
        if headPosition < 0
          then
            set tape to " " + tape
            set headPosition to 0
        end if
    end if
  end procedure

end class

immutable class Rule
  constructor(currentState as String, currentSymbol as String, nextState as String, writeSymbol as String, move as Dir)
    set property.currentState to currentState
    set property.currentSymbol to currentSymbol
    set property.nextState to nextState
    set property.writeSymbol to writeSymbol
    set property.move to move
  end constructor

  property currentState as String

  property currentSymbol as String

  property nextState as String

  property writeSymbol as String

  property move as Dir

  function asString() return String
    return "{currentState},{currentSymbol},{nextState},{writeSymbol},{move}"
  end function

end class

enum Dir
  left, right
end enum
