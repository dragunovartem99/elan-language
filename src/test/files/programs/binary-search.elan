# 3ed44db350cff4c4bfffbca5d3d2126569816b175ddacfd8746fc1faa73ac49b Elan Beta 1 valid

main
  var fruit set to ["apple", "avocado", "banana", "blueberry", "cherry", "fig", "grape", "kiwi", "lemon", "lychee", "mango", "orange", "papaya", "peach", "pear", "pineapple", "plum", "raspberry", "strawberry", "watermelon"]
  var exit set to false
  while not exit
    var wanted set to inputString("What type of fruit do you want ('x' to exit)? ")
    if wanted isnt "x"
      then
        var result set to binarySearch(fruit, wanted)
        if result
          then
            print "We can supply a {wanted}"
          else
            print "Sorry, we cannot supply a {wanted}"
        end if
      else
        set exit to true
    end if
  end while
end main

function binarySearch(list as [String], item as String) return Boolean
  var result set to false
  if list.length() > 0
    then
      var mid set to div(list.length(), 2)
      var value set to list[mid]
      if item is value
        then
          set result to true
        else if isBefore(item, value)
          set result to binarySearch(list[..mid], item)
        else
          set result to binarySearch(list[mid + 1..], item)
      end if
  end if
  return result
end function

test 
  var li1 set to ["lemon", "lime", "orange"]
  assert binarySearch(li1, "lemon") is true
  assert binarySearch(li1, "lime") is true
  assert binarySearch(li1, "orange") is true
  assert binarySearch(li1, "pear") is false
  var li2 set to ["lemon", "orange"]
  assert binarySearch(li2, "lemon") is true
  assert binarySearch(li2, "orange") is true
  assert binarySearch(li2, "pear") is false
  var li3 set to ["lemon"]
  assert binarySearch(li3, "lemon") is true
  assert binarySearch(li3, "lime") is false
  var li4 set to empty [String]
  assert binarySearch(li4, "pear") is false
end test
