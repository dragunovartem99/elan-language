# f3b15749a35bb6db5a9a4f2af43ea86f34d93ef688037801436586a97ec30f0f Elan Beta 2 valid

function bestFitLine(points as {Point}) return (Float, Float)
  let sumX be points.reduce(0.0, lambda s as Float, p as Point => s + p.x)
  let sumXsq be points.reduce(0.0, lambda s as Float, p as Point => s + p.x^2)
  let sumY be points.reduce(0.0, lambda s as Float, p as Point => s + p.y)
  let sumXY be points.reduce(0.0, lambda s as Float, p as Point => s + p.x*p.y)
  let n be points.length()
  let a be (sumY*sumXsq - sumX*sumXY)/(n*sumXsq - sumX*sumX)
  let b be (n*sumXY - sumX*sumY)/(n*sumXsq - sumX*sumX)
  return (a, b)
end function

class Point
  constructor(x as Float, y as Float)
    set property.x to x
    set property.y to y
  end constructor

  property x as Float

  property y as Float

end class

test 
  let l1 be {new Point(0.71, 1.12), new Point(3.56, 5.36), new Point(7.83, 9.04)}
  let result be bestFitLine(l1)
  assert round(result.first(), 3) is 0.766
  assert round(result.second(), 3) is 1.093
end test
