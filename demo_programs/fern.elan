# e7893268c6b90bda1bb0c28194b4cad41cc90c8db5185159144d68cb497b3add Elan Beta 9 valid

# https://en.wikipedia.org/wiki/Barnsley_fern
# Draws a fractal image
main
  variable vg set to new List<of VectorGraphic>()
  # coordinates
  variable x set to 0.0
  variable y set to 0.16
  # random number
  variable r set to 0
  variable rect set to new RectangleVG(0, 0, 0, 0, 0, 0, 0)
  variable count set to 0
  # don't set the count limit too high
  # or the browser will run out of memory
  while count < 1000
    set rect, x, y, r to onepoint(x, y, r)
    call vg.append(rect)
    call displayVectorGraphics(vg)
    set count to count + 1
  end while
  print "Finished"
end main

function onepoint(x as Float, y as Float, r as Int) returns (RectangleVG, Float, Float, Int)
  # next pseudo-random number (Grogono parameters)
  variable nr set to (r*25173 + 13849) mod 0x10000
  variable nx, ny set to onestep(x, y, nr/0x10000)
  let rect be new RectangleVG(nx*scale + 50, 75 - ny*scale, 0.5, 0.5, 0x408040, 0x408040, 0.25)
  return tuple(rect, nx, ny, nr)
end function

# r is random Float 0.0 <= r < 1.0
# returns new x and y
# written in a data-driven style
function onestep(x as Float, y as Float, r as Float) returns (Float, Float)
  # Currently you can't make the params into one long line
  # as it makes the browser use 100% CPU
  # xx xy yx yy cx cy probablity
  let p1 be {0.0, 0.0, 0.0, 0.16, 0.0, 0.0, 0.01}
  let p2 be {0.85, 0.04, -0.04, 0.85, 0.0, 1.60, 0.85}
  let p3 be {0.20, -0.26, 0.23, 0.22, 0.0, 1.60, 0.07}
  let p4 be {-0.15, 0.28, 0.26, 0.24, 0.0, 0.44, 0.07}
  let params be {p1, p2, p3, p4}
  # cumulative probabilty
  variable cumuprob set to 0.0
  variable done set to false
  variable nx set to 0.0
  variable ny set to 0.0
  each pp in params
    set cumuprob to cumuprob + pp[6]
    if (not done) and (r < cumuprob) then
      set nx to x*pp[0] + y*pp[1] + pp[4]
      set ny to x*pp[2] + y*pp[3] + pp[5]
      set done to true
    end if
  end each
  return tuple(nx, ny)
end function

test one
  assert roundtuple2(onestep(0.0, 0.16, 0.5)) is {0.0064, 1.736}
  assert roundtuple2(onestep(0.0, 0.16, 0.9)) is {-0.0416, 1.6352}
end test

# two approaches to rounding a tuple to N decimal places
function roundtuple1(n as (Float, Float)) returns (Float, Float)
  let a, b be n
  return tuple(a.round(8), b.round(8))
end function

function roundtuple2(n as (Float, Float)) returns ListImmutable<of Float>
  let a, b be n
  return {a, b}.map(lambda x as Float => x.round(8))
end function

constant scale set to 7
