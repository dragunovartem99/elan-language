# FFFF Elan Beta 9 valid

main
  variable bubbles set to new List<of CircleVG>()
  # create 20 small bubbles at the bottom
  for i from 1 to 20 step 1
    let randomColour be randomInt(0, white)
    let b be new CircleVG(i*5 + 2, 75, 1, transparent, randomColour, 1)
    call bubbles.append(b)
  end for
  while true
    each b in bubbles
      if random() < 0.05 then
        # 1/20 chance bubble bursts' & replaced by new one at the bottom
        call b.setCentreY(75)
        call b.setRadius(1)
      else
        # bubble rises and grows
        call b.setRadius(b.radius + 0.2)
        call b.setCentreY(b.centreY - 1)
      end if
    end each
    # create a List<of VectorGraphic> from the bubbles (List<of CircleVG>)
    let vgList be new List<of VectorGraphic>()
    each b in bubbles
      call vgList.append(b)
    end each
    call displayVectorGraphics(vgList)
    call pause(50)
  end while
end main
