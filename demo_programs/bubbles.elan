# 4ff1caee211e7c60516613846ec6ce8f6eb7f4119e57fbfee00fdf1ae5e2f36f Elan 0.10.0-Beta guest default_profile valid

main
  # Keep reference to each bubble in two lists: one used for display, one used for changing the bubbles
  let vgs be new List<of VectorGraphic>()
  let bubbles be new List<of CircleVG>()
  # create 20 small bubbles at the bottom
  for i from 1 to 20 step 1
    let randomColour be randomInt(0, white)
    let b be new CircleVG(i*5 + 2, 75, 1, transparent, randomColour, 1)
    call vgs.append(b)
    call bubbles.append(b)
  end for
  # move, grow, and burst bubbles, continuously
  while true
    # Iterate through the 'bubbles' because each is known to be a CircleVG
    each b in bubbles
      if random() < 0.05 then
        # 1/20 chance bubble 'bursts', and is replaced by new one at the bottom
        call b.setCentreY(75)
        call b.setRadius(1)
      else
        # bubble rises and grows
        call b.setCentreY(b.centreY - 1)
        call b.setRadius(b.radius + 0.2)
      end if
    end each
    # Each update to a bubble in 'bubbles' list will also be seen in the `vgs` list, which can be displayed
    call displayVectorGraphics(vgs)
    call pause(50)
  end while
end main
