# 89f2f546218b9e8360db93477a17b7551eed78e93ef8a0b64fa16e4907b38554 Elan Beta 9 valid

main
  variable blocks set to new Array2D<of Int>(40, 30, white)
  variable x set to 20
  variable y set to 15
  while true
    call blocks.put(x, y, red)
    call displayBlocks(blocks)
    call pause(100)
    call blocks.put(x, y, black)
    let dir be randomInt(0, 3)
    call updateXY(x, y, dir)
  end while
end main

procedure updateXY(out x as Int, out y as Int, dir as Int)
  if dir is 0 then
    set x to lesserOf(x + 1, 40)
  else if dir is 1 then
    set x to greaterOf(x - 1, 0)
  else if dir is 2 then
    set y to lesserOf(y + 1, 30)
  else if dir is 3 then
    set y to greaterOf(y - 1, 0)
  end if
end procedure

function lesserOf(value1 as Int, value2 as Int) returns Int
  return if value1 < value2 then value1 else value2
end function

function greaterOf(value1 as Int, value2 as Int) returns Int
  return if value1 > value2 then value1 else value2
end function

test lesserOf
  assert lesserOf(5, 10) is 5
  assert lesserOf(10, 10) is 10
  assert lesserOf(11, 10) is 10
  assert lesserOf(-1, 10) is -1
end test

test greaterOf
  assert greaterOf(7, 3) is 7
  assert greaterOf(3, 3) is 3
  assert greaterOf(2, 3) is 3
  assert greaterOf(-1, 0) is 0
end test
