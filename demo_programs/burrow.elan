# FFFF Elan Beta 6 valid

main
  variable g set to new BlockGraphics()
  variable x set to 20
  variable y set to 15
  while true
    set g to g.withText(x, y, "*", red, black)
    call g.display()
    call pause(100)
    set g to g.withBlock(x, y, black)
    let dir be randomInt(0, 3)
    set x, y to updateCoordinates(x, y, dir)
  end while
end main

function updateCoordinates(x as Int, y as Int, dir as Int) returns (Int, Int)
  variable newX set to x
  variable newY set to y
  switch on dir
  match 0 with
    set newX to lesserOf(x + 1, 39)
  match 1 with
    set newX to greaterOf(x - 1, 0)
  match 2 with
    set newY to lesserOf(y + 1, 29)
  match 3 with
    set newY to greaterOf(y - 1, 0)
  end switch
  return (newX, newY)
end function

function lesserOf(value1 as Int, value2 as Int) returns Int
  variable result set to 0
  if value1 < value2 then
    set result to value1
  else
    set result to value2
  end if
  return result
end function

function greaterOf(value1 as Int, value2 as Int) returns Int
  variable result set to 0
  if value1 > value2 then
    set result to value1
  else
    set result to value2
  end if
  return result
end function

test lesserOf
  assert lesserOf(5, 10) is 5
  assert lesserOf(10, 10) is 10
  assert lesserOf(11, 10) is 10
  assert lesserOf(-1, 10) is -1
end test

test greaterOf
  assert greaterOf(7, 3) is 7
  assert greaterOf(3, 3) is 3
  assert greaterOf(2, 3) is 3
  assert greaterOf(-1, 0) is 0
end test

test updateCoordinates
  # normal
  assert updateCoordinates(3, 7, 0) is (4, 7)
  assert updateCoordinates(3, 7, 1) is (2, 7)
  assert updateCoordinates(3, 7, 2) is (3, 8)
  assert updateCoordinates(3, 7, 3) is (3, 6)
  # edge (keeps within boundaries of Graphics display)
  assert updateCoordinates(0, 7, 1) is (0, 7)
  assert updateCoordinates(39, 7, 0) is (39, 7)
  assert updateCoordinates(3, 0, 3) is (3, 0)
  assert updateCoordinates(3, 29, 2) is (3, 29)
end test
