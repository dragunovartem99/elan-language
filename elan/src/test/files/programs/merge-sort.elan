# d50f1f903fb3ea3ed359326714ae4a3be6e6cae771b99840122236ce901b53a7 Elan v0.1 valid

main
  var li set to {"plum", "apricot", "lime", "lemon", "melon", "apple", "orange", "strawberry", "pear"}
  print mergeSort(li)
end main

function mergeSort(list as {String}) return {String}
  var result set to list
  if list.length() > 1
    var mid set to floor(list.length()/2)
    var len set to list.length()
    set result to merge(mergeSort(list.getRange(0, mid)), mergeSort(list.getRange(mid, len)))
  end if
  return result
end function

function merge(a as {String}, b as {String}) return {String}
  var result set to new ImmutableList<of String>()
  if a.length() is 0
    set result to b
    else if b.length() is 0
      set result to a
    else if isBefore(a.get(0), b.get(0))
      set result to {a.get(0)} + merge(a.getRange(1, a.length()), b)
    else
      set result to {b.get(0)} + merge(a, b.getRange(1, b.length()))
  end if
  return result
end function

test mergeSort
  var li1 set to new ImmutableList<of String>()
  assert mergeSort(li1) is li1
  var li2 set to {"plum"}
  assert mergeSort(li2) is li2
  var li3 set to {"plum", "apricot", "lime", "lemon", "melon", "apple", "orange", "strawberry", "pear"}
  var expected3 set to {"apple", "apricot", "lemon", "lime", "melon", "orange", "pear", "plum", "strawberry"}
  assert mergeSort(li3) is expected3
  var li4 set to {"plum", "apricot", "lime", "lemon", "melon", "apple", "orange", "strawberry"}
  var expected4 set to {"apple", "apricot", "lemon", "lime", "melon", "orange", "plum", "strawberry"}
  assert mergeSort(li4) is expected4
  var li5 set to {"apple", "apricot", "lemon", "lime", "melon", "orange", "pear", "strawberry"}
  assert mergeSort(li5) is li5
end test
