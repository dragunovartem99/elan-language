# 2704bcc6d34eefd212a5dd1c88a0f54af6da6ca5ff3eaf0adc4458f93e4738f9 Elan v0.1 valid

function calcA(sumX as Float, sumXsq as Float, sumY as Float, sumXY as Float, n as Int) return Float
  return (sumY * sumXsq - sumX * sumXY) / (n * sumXsq - sumX * sumX)
end function

function calcSumX(points as List<of (Float, Float)>) return Float
  return points.reduce(0.0, lambda s,p -> s + p.first())
end function

function calcSumXY(points as List<of (Float, Float)>) return Float
  return points.reduce(0.0, lambda s,p ->s + p.first() * p.second())
end function

function calcSumY(points as List<of (Float, Float)>) return Float
  return points.reduce(0.0, lambda s,p -> s + p.second())
end function

function calcSumXsq(points as List<of (Float, Float)>) return Float
  return points.reduce(0.0, lambda s,p -> s + p.first() * p.first())
end function

function bestFitFromPoints(points as List<of (Float, Float)>) return (Float, Float)
  return bestFitFromSummaryTerms(calcSumX(points), calcSumXsq(points), calcSumY(points), calcSumXY(points), points.length())
end function

function calcB(sumX as Float, sumXsq as Float, sumY as Float, sumXY as Float, n as Int) return Float
  return (n * sumXY - sumX * sumY) / (n * sumXsq - sumX * sumX)
end function

function bestFitFromSummaryTerms(sumX as Float, sumX2 as Float, sumY as Float, sumXY as Float, n as Int) return (Float, Float)
  return (calcA(sumX, sumX2, sumY, sumXY, n), calcB(sumX, sumX2, sumY, sumXY, n))
end function

constant example set to {(0, 2.673040396), (1, 3.550437254), (2, 5.063963424), (3, 6.445493589), (4, 5.959864362), (5, 7.826499306), (6, 5.628729698), (7, 10.65942485), (8, 8.010532477), (9, 10.67121885), (10, 9.000669447), (11, 13.50830072), (12, 11.43464406), (13, 13.58275437), (14, 13.10821319), (15, 10.28617997), (16, 17.86384888), (17, 15.20409345), (18, 17.00764549), (19, 12.11596949)}
