# f20e2c142adcae1fd7b4e720079768104477b424a2e02c51082161e43b8d0d2b Elan v0.1 valid

main
  var exit set to false
  while not exit
    print "What type of fruit do you want ('x' to exit)?"
    input wanted
    if wanted is not "x"
      var result set to binarySearch(fruit, wanted)
      if result
        print "We can supply a {wanted}"
        else
          print "Sorry, we cannot supply a {wanted}"
      end if
      else
        set exit to true
    end if
  end while
end main

function binarySearch(list as {String}, item as String) return Boolean
  var result set to false
  if list.length() > 0
    var mid set to div(list.length(), 2)
    var value set to list[mid]
    if item is value
      set result to true
      else if isBefore(item, value)
        set result to binarySearch(list[..mid], item)
      else
        set result to binarySearch(list[mid + 1..], item)
    end if
  end if
  return result
end function

constant fruit set to {"apple", "avocado", "banana", "blueberry", "cherry", "fig", "grape", "kiwi", "lemon", "lychee", "mango", "orange", "papaya", "peach", "pear", "pinapple", "plumb", "raspberry", "strawberry", "watermelon"}
