# 8c71976d675bbee0fece686282e18ee26b1116e383f0fe122f9ed1efe6f8a015 Elan Beta 1 valid

main
  var exit set to false
  while not exit
    print "What type of fruit do you want ('x' to exit)?"
    input wanted
    if wanted is not "x"
      then
        var result set to binarySearch(fruit, wanted)
        if result
          then
            print "We can supply a {wanted}"
          else
            print "Sorry, we cannot supply a {wanted}"
        end if
      else
        set exit to true
    end if
  end while
end main

function binarySearch(list as {String}, item as String) return Boolean
  var result set to false
  if list.length() > 0
    then
      var mid set to div(list.length(), 2)
      var value set to list.get(mid)
      if item is value
        then
          set result to true
        else if isBefore(item, value)
          set result to binarySearch(list.getRange(0, mid), item)
        else
          set result to binarySearch(list.getRange(mid + 1, list.length()), item)
      end if
  end if
  return result
end function

constant fruit set to {"apple", "avocado", "banana", "blueberry", "cherry", "fig", "grape", "kiwi", "lemon", "lychee", "mango", "orange", "papaya", "peach", "pear", "pinapple", "plumb", "raspberry", "strawberry", "watermelon"}
