# 88515fcfe626ca19af6d4c67361e7d883de877ccba93262c8084e86166b80fb1 Elan Beta 2 valid

# This is *not* a card game: it's a demo of how you could *start* to draw cards for such game
main
  var gr set to new Graphics()
  set gr to gr.fill("", black, green)
  var card1 set to new Card(Rank.ace, Suit.hearts)
  var card2 set to new Card(Rank.two, Suit.spades)
  set gr to placeCard(gr, card1, 1, 1)
  set gr to placeCard(gr, card2, 8, 1)
  call gr.draw()
end main

function placeCard(gr as Graphics, c as Card, x as Int, y as Int) return Graphics
  var gr2 set to gr
  for row from y to y + 8 step 1
    for col from x to x + 5 step 1
      set gr2 to gr2.putBackground(col, row, white)
      set gr2 to gr2.putForeground(col, row, c.colour)
    end for
  end for
  set gr2 to gr2.putChar(x, y, c.label)
  set gr2 to gr2.putChar(x + 1, y, c.symbol)
  return gr2
end function

class Card
  constructor(rank as Rank, suit as Suit)
    set property.rank to rank
    set property.suit to suit
    set property.symbol to unicode(symbols.getValueByKey(suit))
    set property.colour to colourForSuit.getValueByKey(suit)
    set property.label to labels.getValueByKey(rank)
  end constructor

  property suit as Suit

  property symbol as String

  property rank as Rank

  property label as String

  property colour as Int

end class

enum Suit clubs, diamonds, hearts, spades

enum Rank ace, one, two, three, four, five, six, seven, eight, nine, ten, jack, queen, king

constant colourForSuit set to {Suit.clubs:black, Suit.diamonds:red, Suit.hearts:red, Suit.spades:black}

constant symbols set to {Suit.clubs:0x2663, Suit.diamonds:0x2666, Suit.hearts:0x2665, Suit.spades:0x2660}

constant labels set to {Rank.ace:"A", Rank.two:"2", Rank.three:"3", Rank.four:"4", Rank.five:"5", Rank.six:"6", Rank.seven:"7", Rank.eight:"8", Rank.nine:"9", Rank.ten:"10", Rank.jack:"J", Rank.queen:"Q", Rank.king:"K"}
