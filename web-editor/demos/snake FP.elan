# c8bd3d016207acc2c05e523f55f35b697780a20084782e361ba0220d25d57923 Elan Beta 2 valid

# Run the program and use the standard W,A,S,D keys to change Snake direction
# Try to 'eat' the (red) apples, and avoid hitting the edge or the body of the snake.
main
  var game set to new Game(firstRandom())
  set game to newApple(game)
  while game.isOn
    set game to updateGraphics(game)
    var gr set to game.graphics
    call gr.draw()
    call pause(150)
    set game to clockTick(game, gr.getKeystroke())
  end while
  print "Game Over! Score: {score(game)}"
end main

function clockTick(g as Game, k as String) return Game
  let g2 be if k is "" then g else copy g with key to k
  let g3 be moveSnake(g2)
  let g4 be eatAppleOrNot(g3)
  return if gameOver(g4) then copy g4 with isOn to false else g4
end function

function updateGraphics(g as Game) return Game
  let g2 be copy g with graphics to g.graphics.putBackground(g.apple.x, g.apple.y, red)
  let g3 be copy g2 with graphics to g2.graphics.putBackground(g2.head.x, g2.head.y, green)
  let tail be g3.body[0]
  let tailColour be if tail is g3.priorTail then green else white
  return copy g3 with graphics to g3.graphics.putBackground(tail.x, tail.y, tailColour)
end function

function newApple(g as Game) return Game
  let x be g.rnd.valueInt(0, 39)
  let rnd2 be g.rnd.next()
  let y be rnd2.valueInt(0, 29)
  return copy g with apple to new Square(x, y), rnd to rnd2.next()
end function

function score(g as Game) return Int
  return g.body.length() - 2
end function

function moveSnake(g as Game) return Game
  let k be g.key
  let x be g.head.x
  let y be g.head.y
  let newX be if k is "a" then x - 1 else if k is "d" then x + 1 else x
  let newY be if k is "w" then y - 1 else if k is "s" then y + 1 else y
  return copy g with body to g.body + g.head, head to new Square(newX, newY)
end function

function eatAppleOrNot(g as Game) return Game
  let tail be g.body[0]
  let moveTail be g.body[1..g.body.length()]
  return if overApple(g) then newApple(g) else copy g with priorTail to tail, body to moveTail
end function

function overApple(g as Game) return Boolean
  return g.head is g.apple
end function

function gameOver(g as Game) return Boolean
  return bodyOverlaps(g, g.head) or headIsAtEdge(g)
end function

function headIsAtEdge(g as Game) return Boolean
  let x be g.head.x
  let y be g.head.y
  return (x is -1) or (y is -1) or (x is 40) or (y is 30)
end function

function bodyOverlaps(g as Game, target as Square) return Boolean
  return g.body.any(lambda s as Square => s is target)
end function

immutable class Game
  constructor(rnd as Random)
    set property.graphics to new Graphics()
    set property.head to new Square(22, 15)
    set property.priorTail to empty Square
    set property.body to {new Square(20, 15), new Square(21, 15)}
    set property.isOn to true
    set property.key to "d"
    set property.rnd to rnd
  end constructor

  property head as Square

  property body as {Square}

  property priorTail as Square

  property apple as Square

  property isOn as Boolean

  property rnd as Random

  property graphics as Graphics

  property key as String

end class

immutable class Square
  constructor(x as Int, y as Int)
    set property.x to x
    set property.y to y
  end constructor

  property x as Int

  property y as Int

end class
